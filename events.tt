#nop List for all events. Will call out to other files.

#alias {gmcp}
{
   #echo {send out request};
   #send {$TELNET[IAC]$TELNET[SB]$TELNET[GMCP] %0 $TELNET[IAC]$TELNET[SE]\};
}

#EVENT {IAC SB GMCP core.ping IAC SE}
{
   #var gmcp[core][ping] {%0};
   #show CorePing: {%0}:;
}

#EVENT {IAC SB GMCP char.items.list IAC SE}
{
   #var gmcp[char][items][list] {%0};
   raiseEvent CharItemsList;
}

#EVENT {IAC SB GMCP char.items.add IAC SE}
{
   #var gmcp[char][items][add] {%0};
   raiseEvent CharItemsAdd;
}

#EVENT {IAC SB GMCP Char.Items.Remove IAC SE}
{
   #var gmcp[char][items][remove] {%0};
   raiseEvent CharItemsRemove;
}


#EVENT {IAC SB GMCP Char.Afflictions.Add IAC SE}
{
   #variable gmcp[char][afflictions][add] {%0};
   #list afflictions ins -1 $gmcp[char][afflictions][add][+1][name];
   raiseEvent updateui;
}

#EVENT {IAC SB GMCP Char.Afflictions.List IAC SE}
{
   #variable gmcp[char][afflictions][list] {%0};

   #list afflictions clear;
   #foreach $gmcp[char][afflictions][list][] aff {
      #list afflictions ins -1 $aff[name];
   }
   raiseEvent updateui;
}

#EVENT {IAC SB GMCP Char.Afflictions.Remove IAC SE}
{
   #variable gmcp[char][afflictions][remove] {%0};
   print Removed Affliction $gmcp[char][afflictions][remove];

   #list afflictions find {$gmcp[char][afflictions][remove]} idx;
   #if {$idx !== 0} { #list afflictions delete $idx; };
   raiseEvent updateui;
}

#EVENT {IAC SB GMCP char.status IAC SE}
{
    #variable gmcp[char][status] {%0};
}

#var prevHp 0;
#var prevMp 0;
#EVENT {IAC SB GMCP char.vitals IAC SE}
{
   #variable gmcp[char][vitals] {%0};

   raiseEvent CharVitals;

   #var hp $gmcp[char][vitals][hp];
   #if {$prevHp == 0} {#var prevHp $hp};

   #if {$prevHp != $hp} {
      #math {diff} {$prevHp - $hp};
      #if {$diff > 0} {#show HP <501>-$diff, Orb: $orbActive};
      #else {#math diff {$diff * -1}; #show HP <128>+$diff, Orb: $orbActive};

      #unvar diff;
      raiseEvent HpChanged;
   };
   #var prevHp $hp;


   #var mp $gmcp[char][vitals][mp];
   #if {$prevMp == 0} {#var prevMp $mp};

   #if {$prevMp != $mp} {
      #math {diff} {$prevMp - $mp};
      #if {$diff > 0} {#show Mana <148>-$diff};
      #else {#math diff {$diff * -1}; #show Mana <158>+$diff};
      #unvar diff;
      raiseEvent MpChanged;
   };
   #var prevMp $mp;

   #var maxhp $gmcp[char][vitals][maxhp];
   #if {$checkReckless == 1 && $hp == $maxhp} {
      #send {CURING PREDICT reckless};
   };
   #var checkReckless 0;

   #if {$hp == 0} {
      mstop;
   };

   raiseEvent healing;
}

#EVENT {IAC SB GMCP char.maxstats IAC SE}
{
    #variable gmcp[char][maxstats] {%0}
}

#EVENT {IAC SB GMCP char.worth IAC SE}
{
    #variable gmcp[char][worth] {%0}
}

#EVENT {IAC SB GMCP Room.Info IAC SE}
{
   #variable gmcp[room][info] {%0};
   #if {$gmcp[room][info][num] == -1}
   {
       #return
   };

   #var clearCharDb 1;

   raiseEvent RoomInfo;
}

#EVENT {IAC SB GMCP char.name IAC SE}
{
   #variable gmcp[char][name] {%0};
   #show {I am $gmcp[char][name]};
}

#EVENT {IAC SB GMCP Room.addplayer IAC SE}
{
   #var clearCharDb 1;

   #variable gmcp[room][addplayer] {%0};
   raiseEvent AddPlayer;
}

#EVENT {IAC SB GMCP Room.Players IAC SE}
{
   #var clearCharDb 1;

   #variable gmcp[room][players] {%0};
   #nop [ Players? {1}{{name}{Cthul}{fullname}{Servitor Cthul}} ];

   raiseEvent RoomPlayers;
}

#EVENT {IAC SB GMCP Room.removeplayer IAC SE}
{
   #variable gmcp[room][removeplayer] {%0};
   raiseEvent RoomRemovePlayers;

   #var clearCharDb 1;
}

#EVENT {IAC SB GMCP Char.defences.add IAC SE}
{
   #variable gmcp[char][defences][add] {%0};

   #if {"$gmcp[char][defences][add][name]" == "arctar"} { #var orbActive 1; }
}

#EVENT {IAC SB GMCP Char.defences.list IAC SE}
{
   #variable gmcp[char][defences][list] {%0};
   #nop #show {%0};
   #foreach $gmcp[char][defences][list] def {
      #if {"$def[name]" == "selfishness"} {
         #var selfishness 1;
      }
   }
}

#EVENT {IAC SB GMCP Char.defences.remove IAC SE}
{
   #variable gmcp[char][defences][remove] {%0};
   #nop #show {%0};

   #foreach {$gmcp[char][defences][remove][]} def {
      #nop #show {..: $def :..};
      #if {"$def" == "arctar"} { #var orbActive 0; };
   };
}

#EVENT {RECEIVED OUTPUT}
{
   #nop %0 raw, %1 plain text;
   #if {$clearCharDb} {
      #class chardatabase load;
   };
}

#EVENT {RECEIVED PROMPT}
{
   #nop %0 raw, %1 plain text;
   #if {$clearCharDb} {
      #var clearCharDb 0;
      #class chardatabase clear;
   };

   raiseEvent prompt;
}

#EVENT {SESSION DISCONNECTED}
{
    #nop #map write gmcp.map;
}


#EVENT {CLASS DEACTIVATED battlerage}
{
   #nop deregisterEvent CharVitals;
}

